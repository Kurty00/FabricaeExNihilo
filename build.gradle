plugins {
    id 'fabric-loom' version '0.11-SNAPSHOT'
    id 'maven-publish'
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

repositories {
    mavenCentral()

    maven { url "https://maven.fabricmc.net/" }
    maven { url "https://maven.shedaniel.me/" }
    maven { url "https://www.cursemaven.com" }
    maven { url "https://jitpack.io" }
    maven { url "https://maven.terraformersmc.com/" }
    maven { url "https://maven.saps.dev/minecraft" }
    maven { url "https://maven.bai.lol/" }
    maven { url "https://maven.wispforest.io" }
}

dependencies {
    minecraft "com.mojang:minecraft:$minecraft_version"
    mappings "net.fabricmc:yarn:$yarn_mappings:v2"
    modImplementation "net.fabricmc:fabric-loader:$loader_version"

    modImplementation "net.fabricmc.fabric-api:fabric-api:$fabric_version"

    // Roughly Enough Items
    modCompileOnly "me.shedaniel:RoughlyEnoughItems-api-fabric:$rei_version"
    modRuntimeOnly "me.shedaniel:RoughlyEnoughItems-fabric:$rei_version"

    modImplementation("dev.latvian.mods:kubejs-fabric:$kubejs_version")

    modImplementation "com.terraformersmc:modmenu:$modmenu_version"

    // Change to toggle between wthit and the one probe
    var wthit = false

    // WTHIT and Megane
    modCompileOnly "mcp.mobius.waila:wthit-api:fabric-$wthit_version"
    modCompileOnly "lol.bai.megane:megane-api:7.1.0"
    if (wthit) {
        print "Using wthit!"
        modRuntimeOnly "mcp.mobius.waila:wthit:fabric-$wthit_version"
        modRuntimeOnly "lol.bai:badpackets:fabric-0.1.2"
        modRuntimeOnly "lol.bai.megane:megane-runtime:7.1.0"
        modRuntimeOnly "lol.bai.megane:megane-vanilla:7.1.0"
        modRuntimeOnly "lol.bai.megane:megane-api:7.1.0"
    }

    // The one probe
    modCompileOnly "mcjty.theoneprobe:theoneprobe-fabric:$theoneprobe_version"
    if (!wthit) {
        print "Using the one probe!"
        modRuntimeOnly "mcjty.theoneprobe:theoneprobe-fabric:$theoneprobe_version"
        modRuntimeOnly "com.electronwill.night-config:toml:3.6.0"
    }
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    it.options.release = 17
}

java {
    withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}
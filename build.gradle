plugins {
    id 'fabric-loom' version '0.12-SNAPSHOT'
    id 'maven-publish'
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.archives_base_name
version = project.mod_version + "+mc" + project.minecraft_version
group = project.maven_group

repositories {
    mavenCentral()

    maven { url "https://maven.fabricmc.net/" }
    maven { url "https://maven.shedaniel.me/" }
    maven { url "https://jitpack.io" }
    maven { url "https://maven.terraformersmc.com/" }
    maven { url "https://maven.saps.dev/minecraft" }
    maven { url "https://maven.bai.lol/" }
    maven { url "https://maven.wispforest.io" }
    maven {
        url "http://maven.modmuss50.me/"
        allowInsecureProtocol = true
        content {
            includeGroup "TechReborn"
        }
    }
    maven {
        url "https://api.modrinth.com/maven"
        content {
            includeGroup "maven.modrinth"
        }
    }
    maven {
        url "https://www.cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
}

var settings = new Object() {
    boolean wthit
    boolean megane
    boolean modmenu
    boolean rei
    boolean theOneProbe
    boolean kubejs
    boolean techReborn
    boolean industrialRevolution
    boolean modernIndustrialization
    boolean jade
}
apply from: "config.gradle", to: settings

dependencies {
    minecraft "com.mojang:minecraft:$minecraft_version"
    mappings "net.fabricmc:yarn:$yarn_mappings:v2"
    modImplementation "net.fabricmc:fabric-loader:$loader_version"

    modImplementation "net.fabricmc.fabric-api:fabric-api:$fabric_version"

    modCompileOnly "me.shedaniel:RoughlyEnoughItems-api-fabric:$rei_version"
    modCompileOnly "dev.latvian.mods:kubejs-fabric:$kubejs_version"
    modCompileOnly "com.terraformersmc:modmenu:$modmenu_version"

    if (settings.rei) modRuntimeOnly "me.shedaniel:RoughlyEnoughItems-fabric:$rei_version"
    if (settings.kubejs) modRuntimeOnly "dev.latvian.mods:kubejs-fabric:$kubejs_version"
    if (settings.modmenu) modRuntimeOnly "com.terraformersmc:modmenu:$modmenu_version"
    if (settings.techReborn) modRuntimeOnly "TechReborn:TechReborn-1.18:$tech_reborn_version"
    if (settings.modernIndustrialization) modRuntimeOnly "maven.modrinth:modern-industrialization:$modern_industrialization_version"

    // WTHIT and Megane
    modCompileOnly "mcp.mobius.waila:wthit-api:fabric-$wthit_version"
    if (settings.wthit) {
        modRuntimeOnly "mcp.mobius.waila:wthit:fabric-$wthit_version"
    }
    modCompileOnly "lol.bai.megane:megane-api:7.1.0"
    if (settings.megane) {
        modRuntimeOnly "lol.bai:badpackets:fabric-0.1.2"
        modRuntimeOnly "lol.bai.megane:megane-runtime:$megane_version"
        modRuntimeOnly "lol.bai.megane:megane-vanilla:$megane_version"
        modRuntimeOnly "lol.bai.megane:megane-api:$megane_version"
    }

    // The one probe
    modCompileOnly "mcjty.theoneprobe:theoneprobe-fabric:$theoneprobe_version"
    if (settings.theOneProbe) {
        modRuntimeOnly "mcjty.theoneprobe:theoneprobe-fabric:$theoneprobe_version"
        modRuntimeOnly "com.electronwill.night-config:toml:3.6.0"
    }

    modCompileOnly "curse.maven:jade-324717:3823077"
    if (settings.jade) {
        modRuntimeOnly "curse.maven:jade-324717:3823077g"
    }
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    it.options.release = 17
}

java {
    withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}